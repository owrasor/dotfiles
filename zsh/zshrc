# # Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# # Initialization code that may require console input (password prompts, [y/n]
# # confirmations, etc.) must go above this block; everything else may go below.
# if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
#   source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
# fi

export ZSH="$HOME/.oh-my-zsh"
# ZSH_THEME="powerlevel10k/powerlevel10k"
HYPHEN_INSENSITIVE="true"
COMPLETION_WAITING_DOTS="true"
HIST_STAMPS="mm/dd/yyyy"
VI_MODE_SET_CURSOR=true
VI_MODE_RESET_PROMPT_ON_MODE_CHANGE=true

#Need this custom plugin to add artisan commands
#https://github.com/owrasor/zsh-artisan
plugins=(
    artisan
    npm
    composer
    cp
    dnf
    docker
    docker-compose
    git
    httpie
    rsync
    tmux
    z
    nvm
)

source $ZSH/oh-my-zsh.sh

#--------------------------------------------------------------------------
# Configuration
#--------------------------------------------------------------------------

# Decrease delay that vi-mode waits for the end of a key sequence
export KEYTIMEOUT=15

typeset -U path cdpath fpath
path=(
    $HOME/.local/bin
    $HOME/.config/composer/vendor/bin
    $HOME/.go/bin
    $HOME/.cargo/bin
    $HOME/App/PhpStorm-231.8109.199/bin
    ./vendor/bin
    ${ANDROID_HOME}tools/
    ${ANDROID_HOME}platform-tools/
    $path
)

setopt auto_cd
cdpath=(
    $HOME/Code
)

zstyle ':completion:*' group-name ''
zstyle ':completion:*:descriptions' format %d
zstyle ':completion:*:descriptions' format %B%d%b
zstyle ':completion:*:complete:(cd|pushd):*' tag-order \
    'local-directories named-directories'

export EDITOR=vim
export GIT_EDITOR=vim
export FZF_DEFAULT_COMMAND='ag -u -g ""'

unsetopt sharehistory

# Example aliases
alias vim="nvim"
alias sail='[ -f sail ] && sail || vendor/bin/sail'

# Laravel
alias a="artisan"
alias tinker="artisan tinker"
alias serve="artisan serve"
alias mfs="artisan migrate:fresh --seed"

# Git
alias g="git"
alias gs="git s"
alias nah="git reset --hard;git clean -df"
alias co="git checkout"
alias main='git checkout $([ `git rev-parse --quiet --verify master` ] && echo "master" || echo "main")'
alias glg='git log --oneline --decorate --color --graph'
alias gclean='git branch --merged | grep -v "\*" | xargs -n 1 git branch -d'

# Docker
alias d="docker"
alias dc="docker-compose"

#PHPSTORM
alias pstorm="phpstorm.sh"

open () {
    xdg-open $* > /dev/null 2>&1
}

composer-link() {
    composer config repositories.local '{"type": "path", "url": "'$1'"}' --file composer.json
}

# Restore a PostgreSQL database dump using standard psql credentials
restore_db() {
    local use_docker=0
    local container_name="postgres"

    if [[ "$1" == "-wd" ]]; then
        use_docker=1
        shift
    fi

    if [[ $# -ne 2 ]]; then
        echo "Usage: restore_pg [-wd] <database> <dump_path>"
        return 1
    fi

    if (( ! use_docker )) && ! command -v psql >/dev/null 2>&1; then
        echo "psql command not found"
        return 1
    fi

    local db_name="$1"
    local dump_path="$2"

    if [[ ! -f "$dump_path" ]]; then
        echo "Dump file not found: $dump_path"
        return 1
    fi

    local -a psql_cmd
    local -a psql_restore_cmd

    if (( use_docker )); then
        if ! command -v docker >/dev/null 2>&1; then
            echo "docker command not found"
            return 1
        fi

        local resolved_container="$container_name"

        if ! docker ps --format '{{.Names}}' | grep -Fx "$resolved_container" >/dev/null 2>&1; then
            resolved_container="$(docker ps --filter "label=com.docker.compose.service=${container_name}" --format '{{.Names}}' | head -n 1)"

            if [[ -z "$resolved_container" ]]; then
                resolved_container="$(docker ps --filter "name=${container_name}" --format '{{.Names}}' | head -n 1)"
            fi

            if [[ -z "$resolved_container" ]]; then
                resolved_container="$(docker ps --filter "ancestor=${container_name}" --format '{{.Names}}' | head -n 1)"
            fi

            if [[ -z "$resolved_container" && "$container_name" != "postgres" ]]; then
                resolved_container="$(docker ps --filter "ancestor=postgres" --format '{{.Names}}' | head -n 1)"
            fi

            if [[ -z "$resolved_container" ]]; then
                echo "Running container not found matching identifier: $container_name"
                return 1
            fi
        fi

        resolved_container="${resolved_container//$'\n'/}"

        psql_cmd=(docker exec -i "$resolved_container" env PGPASSWORD="secret" psql --username=homestead --set=ON_ERROR_STOP=on)
        psql_restore_cmd=(docker exec -i "$resolved_container" env PGPASSWORD="secret" psql --username=homestead --set=ON_ERROR_STOP=off)
    else
        psql_cmd=(env PGPASSWORD="secret" psql --username=homestead --set=ON_ERROR_STOP=on)
        psql_restore_cmd=(env PGPASSWORD="secret" psql --username=homestead --set=ON_ERROR_STOP=off)
    fi

    local admin_db="postgres"
    local escaped_db_literal="${db_name//\'/''}"
    local escaped_db_identifier="${db_name//\"/\"\"}"
    local db_exists_output

    if ! db_exists_output="$("${psql_cmd[@]}" --dbname="$admin_db" --tuples-only --no-align --command "SELECT 1 FROM pg_database WHERE datname = '${escaped_db_literal}'")"; then
        echo "Failed to verify database existence: $db_name"
        return 1
    fi

    local db_exists="${db_exists_output//$'\n'/}"

    if [[ "$db_exists" == "1" ]]; then
        local terminate_query="SELECT pg_terminate_backend(pid) FROM pg_stat_activity WHERE datname = '${escaped_db_literal}' AND pid <> pg_backend_pid();"

        if ! "${psql_cmd[@]}" --dbname="$admin_db" --command "$terminate_query"; then
            echo "Failed to terminate active sessions for database: $db_name"
            return 1
        fi

        if ! "${psql_cmd[@]}" --dbname="$admin_db" --command "DROP DATABASE \"${escaped_db_identifier}\";"; then
            echo "Failed to drop existing database: $db_name"
            return 1
        fi
    fi

    if ! "${psql_cmd[@]}" --dbname="$admin_db" --command "CREATE DATABASE \"${escaped_db_identifier}\";"; then
        echo "Failed to create database: $db_name"
        return 1
    fi

    if (( use_docker )); then
        if ! "${psql_restore_cmd[@]}" --dbname="$db_name" < "$dump_path"; then
            echo "Restore failed inside container ${resolved_container:-$container_name}"
            return 1
        fi
    else
        if ! "${psql_restore_cmd[@]}" --dbname="$db_name" --file="$dump_path"; then
            echo "Restore failed for database $db_name"
            return 1
        fi
    fi
}

#--------------------------------------------------------------------------
# Miscellaneous
#--------------------------------------------------------------------------

### Fix slowness of pastes with zsh-syntax-highlighting.zsh
pasteinit() {
  OLD_SELF_INSERT=${${(s.:.)widgets[self-insert]}[2,3]}
  zle -N self-insert url-quote-magic # I wonder if you'd need `.url-quote-magic`?
}

pastefinish() {
  zle -N self-insert $OLD_SELF_INSERT
}
zstyle :bracketed-paste-magic paste-init pasteinit
zstyle :bracketed-paste-magic paste-finish pastefinish
### Fix slowness of pastes

### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

### End of Zinit's installer chunk
zinit light zdharma/fast-syntax-highlighting
zinit light zsh-users/zsh-autosuggestions
zinit light zsh-users/zsh-completions

export PATH="$PATH:/opt/nvim-linux-x86_64/bin"

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

export PHPVM_DIR="/home/owras_picyvzc/.phpvm"
export PATH="$PHPVM_DIR/bin:$PATH"
[ -s "$PHPVM_DIR/phpvm.sh" ] && . "$PHPVM_DIR/phpvm.sh"

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
source ~/powerlevel10k/powerlevel10k.zsh-theme
